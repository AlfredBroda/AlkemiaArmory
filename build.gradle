import groovy.json.JsonSlurper

plugins {
    id 'java'
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/OWNER/REPOSITORY")
    }
    ivy {
        url 'https://github.com/'
        patternLayout {
            artifact '/[organization]/[module]/releases/download/[revision]/[ext]'
        }
        metadataSources {
            artifact()
        }
    }
}

// project level config
java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

// where to find sources
sourceSets.main.java.srcDirs = ['src/']

// build everything and make a jar
build {
    doLast {
        copy {
            from jar
            into 'jars'
        }
    }
}

def modName = "Alkemia Armoury"
def baseFileName = "alkemia-armoury"
def modFolderName = "${project.name}"
def starsectorDirectory = "../.."

def starsectorCoreDirectory = "${starsectorDirectory}/starsector-core"
def starsectorModDirectory = "${starsectorDirectory}/mods"

def libDir = "${starsectorModDirectory}"
if (new File(starsectorModDirectory).exists()) {
    println("$starsectorModDirectory exists, adding mod folder dependencies.")
} else {
    libDir = "libs"
    println("$starsectorModDirectory does not exist, using $libDir for dependencies.")
}

def modInModsFolder = "$starsectorModDirectory/${modFolderName}"
def jarFileName =  "${baseFileName}.jar"


jar {
    archiveFileName = jarFileName
}



// compile time dependencies
dependencies {
    implementation "jaghaimo:starsector-api:0.95.1a-RC6@starfarer.api.jar"

    // game dependencies
    implementation group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.10'
    implementation group: 'org.lwjgl.lwjgl', name: 'lwjgl', version: '2.9.3'
    implementation group: 'org.lwjgl.lwjgl', name: 'lwjgl_util', version: '2.9.3'
    implementation group: 'log4j', name: 'log4j', version: '1.2.9'
    implementation group: 'org.json', name: 'json', version: '20090211'
    implementation group: 'net.java.jinput', name: 'jinput', version: '2.0.7'
    implementation group: 'org.codehaus.janino', name: 'janino', version: '3.0.7'

    // lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    compileOnly files("$libDir/lw_lazylib/jars/LazyLib.jar")
    compileOnly files("$libDir/LazyLib/jars/LazyLib.jar")
    compileOnly files("$libDir/MagicLib/jars/MagicLib.jar")
    // compileOnly files("$libDir/nexerelin/jars/ExerelinCore.jar")
}

// custom tasks
task release(type: Zip) {
    dependsOn build
    archiveFileName = "${baseFileName}-${readModInfoJson().version}.zip"
    destinationDirectory = file(rootDir)
    from fileTree(".").matching {
        include("jars/*")
        include("data/**/*")
        include("graphics/**/*")
        include("mod_info.json")
        include("${baseFileName}.version")
    }
    into project.name
}

// methods
def readModInfoJson() {
    def jsonFile = file('mod_info.json')
    def slurper = new JsonSlurper()
    return slurper.parse(jsonFile)
}
